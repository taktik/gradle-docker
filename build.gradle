apply plugin: 'jacoco'

buildscript {
    repositories {
        maven {
            credentials {
                username repoUsername
                password repoPassword
            }
            url = mavenRepository
        }
    }

    dependencies {
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.10.0'
        classpath 'org.jfrog.buildinfo:build-info-extractor-gradle:2.2.5'
        classpath 'com.taktik.gradle:gradle-plugin-maven-repository:1.0.0-SNAPSHOT'
    }
}

apply plugin: 'groovy'
apply plugin: 'maven-repository'

repositories {
  maven {
      url = "https://jcenter.bintray.com"
  }
}

sourceSets {
    integTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integTestCompile.extendsFrom testCompile
    integTestRuntime.extendsFrom testRuntime
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()

    implementation 'com.google.guava:guava:17.0'
    implementation 'com.github.docker-java:docker-java:2.2.0'

    testImplementation 'junit:junit-dep:4.11'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation 'org.mockito:mockito-core:1.9.5'
}

task integTest(type: Test) {
    group = 'verification'
    description = 'Runs the integration tests.'
    testClassesDirs = sourceSets.integTest.output.classesDirs
    classpath = sourceSets.integTest.runtimeClasspath

    //reports.html.dir = file("${reporting.baseDir}/integTests")
    //reports.junitXml.destination = file("$buildDir/integTest-results")
}

// run integration tests after unit tests if both are scheduled for execution
integTest.mustRunAfter test
integTest.dependsOn publishToMavenLocal

check.dependsOn(integTest)

jacocoTestReport {
	reports {
		xml.enabled = true
		html.enabled = true
	}
}

/* Workaround for JDK bug (https://bugs.openjdk.java.net/browse/JDK-8051012)
   regression bug in 7u65, fixed in 7u80 and 8u40
 */
compileTestGroovy.groovyOptions.forkOptions.jvmArgs = ['-noverify']
test.jvmArgs '-noverify'
integTest.jvmArgs '-noverify'
